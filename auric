#!/usr/bin/perl

use strict;
use warnings;

package Auric;

use File::Glob;
use IO::Handle;
use IPC::Open3 qw(open3);
use MP3::Tag;
use CDDB_get qw(get_cddb);
use Cwd;
use File::Find qw(find);
use POSIX qw(strftime);
use Term::ReadKey;
use Getopt::Long
    qw(:config posix_default gnu_compat require_order bundling no_ignore_case);

use constant DEBUG => 0;
use constant VERSION => '0.06';

sub usage;
sub fatal;

(my $prog = $0) =~ s{.+/}{};
my $music_dir;
my $clear_to_eol = `tput el`;

if (!caller) {
    __PACKAGE__->run;
}

sub run {
    my ($cls, %opt) = @_;
    %opt = ( $cls->defaults, %opt );
    GetOptions(
        'm|music=s' => \$opt{'music_dir'},
        'd|dir=s' => \$opt{'dot_dir'},
    ) or usage;
    my $cmd = @ARGV ? shift @ARGV : 'help';
    my $self = bless {
        'config' => { $cls->defaults, %opt },
    }, $cls;
    $self->init;
    my $code = $self->can('cmd_' . $cmd) or usage;
    $code->($self);
}

sub device { @_ > 1 ? $_[0]->{'config'}{'device'} = $_[1] : $_[0]->{'config'}{'device'} }

sub defaults {
    return (
        'device' => '/dev/cdrom',
    );
}

sub init {
    my ($self) = @_;
    my $conf = $self->{'config'};
    $music_dir  = $conf->{'music_dir'} ||= $ENV{'MUSIC'} || $ENV{'HOME'} . '/music';
    xchdir($music_dir);
    my $dot_dir = $conf->{'dot_dir'} ||= $music_dir . '/.auric';
    $self->read_config($dot_dir . '/conf');
}

sub read_config {
    my ($self, $dir) = @_;
    my $conf = $self->{'config'};
    my $f = "$dir/auric.conf";
    my $fh = xopen('<', $f);
    while (<$fh>) {
        next if /^\s*(#.*)?$/;  # Skip blank lines and comments
        chomp;
        die "Bad config line: $_" if !/^([-\w]+) +(.*)$/;
        my ($key, $val) = ($1, decode($2));
        $key =~ tr/-/_/;
        $conf->{$key} = $val;
    }
    close $fh;
}

sub dispatch {
    my ($self) = @_;
}

sub decode {
    # TODO: Allow for quotes, etc.?
    return $_[0];
}

sub config {
    my $self = shift;
    my $key = shift;
    my $conf = $self->{'config'};
    return @_ ? $conf->{$key} = shift() : $conf->{$key};
}

sub wait_for_disc {
    print STDERR "\e[1m** Insert the next disc or press Q to stop **\e[0m ";
    while (1) {
        local $_ = `cd-discid 2> /dev/null`;
        if (defined $_ && length $_) {
            chomp;
            my ($id, $count, @etc) = split / /;
            print STDERR "\r", $clear_to_eol;
            return $id;
        }
        ReadMode 4;
        my $key = ReadKey(1);
        ReadMode 0;
        next if !defined $key || $key !~ /[Qq\x03]/;
        chomp $key;
        print STDERR $key, "\n";
        return;
    }
}

sub eject_disc {
    `eject`;
}

sub rand_hash {
    use Digest;
    my $hash = Digest->new('SHA-1');
    $hash->add($$, rand, time);
    return $hash->hexdigest;
}

sub now {
    return strftime('%Y-%m-%d %H:%M:%S', localtime);
}

# --- Command handlers

sub cmd_rip {
    my ($self) = @_;
    my $dev = $self->device;
    my $fhlog = xopen('>>', 'rip.log');
    printf $fhlog "%s Begin session\n", now();
    while (1) {
        my $id = $self->wait_for_disc;
        last if !defined $id;
        my $disc_dir = "discs/$id";
        if (-e "$disc_dir/audio.cddb" || (-d $disc_dir && !rmdir $disc_dir)) {
            print STDERR "\e[32;1mDisc $id appears to have been ripped already\e[0m\n";
            $self->eject_disc;
            next;
        }
        $self->_rip_disc($id, $fhlog);
    }
    printf $fhlog "%s End of session\n", now();
    close $fhlog;
}

sub _rip_disc {
    my ($self, $id, $fhlog) = @_;
    my $job = $self->rand_hash;
    print STDERR "\e[35;1mJob $job\e[0m\n" if DEBUG;
    foreach ('job', $job) {
        xmkdir($_);
        xchdir($_);
    }
    my $t0 = time;
    my $id_again = $self->cdda2wav($fhlog);
    my $tn = time;
    $self->eject_disc;
    xrename("$music_dir/job/$job", "$music_dir/discs/$id");
    printf $fhlog "  ok Disc %s : %2dm %2ds\n", $id, int(($tn - $t0) / 60), (($tn - $t0) % 60);
    call_hook('hook.ripped', "$music_dir/discs/$id");
    xchdir($music_dir);
}

sub cmd_ingest {
    my ($self) = @_;
    my %opt;
    GetOptions(
        'n|dry-run' => \$opt{'dry_run'},
        'f|force'   => \$opt{'force'},
        'e|encode'  => \$opt{'encode'},
        'a|all'     => \$opt{'all'},
        'v|verbose' => \$opt{'verbose'},
    ) or usage;
    if ($opt{'all'}) {
        usage if @ARGV;
        @ARGV = map { basename($_) } glob('discs/*');
    }
    foreach my $disc (@ARGV) {
        while (!-d $disc) {
            $disc =~ s{^$music_dir/(?=.+)}{} or fatal "no such disc $disc";
        }
        if (my $res = $self->_ingest_disc($disc, %opt)) {
            print STDERR "[ok  ] $disc\n";
        }
        elsif ($opt{'verbose'}) {
            print STDERR "[skip] $disc\n";
        }
    }
}

sub cmd_encode {
    my ($self) = @_;
    my $dry_run;
    GetOptions(
        'n|dry-run' => \$dry_run,
    ) or usage;
    foreach my $obj (@ARGV) {
        my @masters = glob("$obj/*/master.wav");
        $self->_encode($obj, @masters) if @masters;
    }
}

sub _start_lame {
    my ($self, $wav, $mp3) = @_;
    open my $fh, '-|', qw(stderr /dev/stdout lame --nohist --preset standard), $wav, $mp3
        or fatal "start lame: $!";
    return $fh;
}

sub _encode {
    my $self = shift;
    my $obj = shift;
    my $objname = basename($obj);
    my $n = scalar @_;
    my @mp3s;
    my $fmt = "\r[%-4.4s] %-3.3s%s %s";
    my ($meta, $tracks, $meta_mtime);
    if (-e "$obj/metadata") {
        ($meta, $tracks) = read_metadata("$obj/metadata");
        $meta_mtime = mtime("$obj/metadata");
    }
    foreach my $wav (@_) {
        my $dir = dirname($wav);
        my $id = basename($dir);
        (my $mp3 = $wav) =~ s{(?<=/)master\.wav$}{listen.mp3};
        (my $tags = $wav) =~ s{(?<=/)master\.wav$}{tags};
        my $had_mp3 = -e $mp3;
        if (!$had_mp3) {
            push @mp3s, $mp3;
            printf STDERR $fmt, $id, sprintf('%3d', 0), '%', $objname;
            my $fh = $self->_start_lame($wav, $mp3);
            while (<$fh>) {
                $/ = "\r" if /ETA/;
                if (/(\d+)%/) {
                    printf STDERR $fmt, $id, sprintf('%3d', $1), '%', '';
                    if ($1 == 100) {
                        close $fh;
                        last;
                    }
                }
            }
        }
        if ($meta && (!-e $tags || $meta_mtime > mtime($tags))) {
            write_tags($tags, $tracks->{$id}, $meta);
        }
        next if !-e $tags;
        if (!$had_mp3 || mtime($mp3) < mtime($tags)) {
            my $h = read_tags($tags);
            MP3::Tag->new($mp3)->update_tags($h);
        }
    }
    printf STDERR $fmt, 'done', '', '', "$objname\n";
    return @mp3s;
}

sub cmd_play {
    my ($self) = @_;
    my @files;
    my %opt = (
        'follow' => 1,
        'follow_skip' => 2,
        'wanted' => sub {
            push @files, $File::Find::name if /^listen\.\w+$/;
        },
        'follow' => 1,
    );
    find(\%opt, @ARGV);
    if (@files == 1 && $files[0] =~ /\.url$/) {
        exec(qw(mplayer), @files);
    }
    else {
        exec(qw(mocp -c -p -a), @files);
    }
}

sub cmd_find {
    my ($self) = @_;
    my ($word, $field);
    my $insensitive = 1;
    GetOptions(
        'w|word-regexp' => \$word,
        'f|field=s' => \$field,
        'I' => sub { $insensitive = 0 },
    ) or usage;
    usage if !@ARGV;
    my $pfx = $insensitive ? '(?i)' : '';
    my @patterns = map { qr/$pfx$_/ } @ARGV;
    my @indexes;
    if ($field) {
        @indexes = map { basename($_) } grep { -e } "index/$field";
        usage if !@indexes;
    }
    else {
        @indexes = map { basename($_) } grep { -f } glob('index/*');
        usage if !@indexes;
    }
    foreach my $index (@indexes) {
        my $fh = xopen('<', "index/$index");
        while (<$fh>) {
            next if !/^(\S+) (.+)/;
            my ($obj, $val) = ($1, $2);
            print "$obj $index $val\n" if grep { $val =~ $_ } @patterns;
        }
        close $fh;
    }
}

sub cmd_ls {
    my ($self) = @_;
    my $twidth = ($ENV{'COLUMNS'} || 80) - 28;
    #@ARGV = $self->dirs if !@ARGV;
    #foreach my $obj (@ARGV) {
    $self->forobj(@ARGV, sub {
        my ($id) = @_;
        my $title = '(no title)';
        my @tracks;
        if (-e 'metadata') {
            my ($meta, $tracks) = read_metadata('metadata');
            $title = $meta->{title} || $meta->{album} || $title;
            chomp $title;
            @tracks = keys %$tracks;
        }
        my $stat = status_string(@tracks);
        printf "%4d %-16.16s %5.5s %-${twidth}s\n", scalar(@tracks), $id, $stat, $title;
    });
}

sub status_string {
    my $str = '-----';
    my $mod_metadata = mtime('metadata') || 0;
    substr($str, 0, 1) = 'm' if $mod_metadata;

    my %mod_tags = map { $_ => (mtime(sprintf('%04d/tags', $_)) || 0) } @_;
    my %mod_listen = map { $_ => (mtime(sprintf('%04d/listen.mp3', $_)) || 0) } @_;

    my $num_tags = scalar grep { $_ > 0 } values %mod_tags;
    my $num_listen = scalar grep { $_ > 0 } values %mod_listen;

    my $min_listen = min(values %mod_listen);
    my $min_tags = min(values %mod_tags);

    my $max_listen = max(values %mod_listen);
    my $max_tags = max(values %mod_tags);

    if ($num_listen) {
        substr($str, 1, 1) = ( $num_listen == @_ ) ? 'l' : 'L';
    }
    if ($num_tags) {
        my $num_new_listen = scalar grep { $mod_listen{$_} > $mod_tags{$_} } @_;
        substr($str, 2, 1) = ( $num_new_listen == @_ ? 't' : 'T' );
    }
    return $str;
}

sub read_file {
    my ($f) = @_;
    my $fh = xopen('<', $f);
    local $/;
    my $str = <$fh>;
    close $fh;
    return $str;
}

sub dirs {
    my ($self) = @_;
    my %dir = map {
        -d $_ ? ($_ => 1)
              : ()
    } glob("$music_dir/*"), glob("$music_dir/discs/*");
    delete $dir{"$music_dir/discs"};
    return sort keys %dir;
}

sub cmd_reindex {
    my ($self) = @_;
    my @indexes = grep { -f } glob('index/*');
    my %fh = map {
        my $fh = xopen('>', $_);
        basename($_) => $fh;
    } @indexes;
    my @discs = glob('discs/*');
    foreach my $disc (@discs) {
        my $fh = xopen('<', "$disc/metadata");
        printf STDERR "[%-4.4s] %s", '', $disc;
        while (<$fh>) {
            next if /^\s*(#.*)?$/;
            chomp;
            my ($id, $key, $val) = split / /, $_, 3;
            my $fhi = $fh{$key} || next;
            if ($id eq '.') {
                print $fhi "$disc $val\n";
            }
            else {
                printf STDERR "\r[%-4.4s]", 0, $disc;
                print $fhi "$disc/$id $val\n";
            }
        }
        close $fh;
        print STDERR "\r[ ok ] $disc\n";
    }
    close $_ for values %fh;
}

sub forobj {
    my $self = shift;
    my $code = pop;
    xchdir($music_dir);
    my @ids = map { s{^($music_dir/)}{}o; $_ } @_ ? @_ : glob('discs/*');
    foreach my $id (@ids) {
        xchdir("$music_dir/$id");
        $code->($id);
    }
}

sub cmd_tag {
    my ($self) = @_;
    my ($verbose, $dry_run);
    GetOptions(
        'n|dry-run' => sub { $dry_run = $verbose = 1 },
        'v|verbose' => \$verbose,
    ) or usage;
    $self->forobj(@ARGV, sub {
        my ($id) = @_;
        print STDERR "disc $id\n\n" if $dry_run;
        my %seen;
        find({
            'follow' => 1,
            'follow_skip' => 2,
            'wanted' => sub {
                print STDERR $_, "\n" if $verbose;
                return if $seen{$File::Find::dir}++;
                if ($_ eq 'listen.mp3' && -e 'tags') {
                    print STDERR basename($File::Find::dir), "\n";
                    return if $dry_run;
                    my $mp3 = MP3::Tag->new($_);
                    my $h = read_tags('tags');
                    $mp3->update_tags($h);
                }
            },
        }, '.');
    });
}

sub cmd_mkpls {
    my ($self) = @_;
    my ($force, $dry_run);
    GetOptions(
        'f|force' => \$force,
        'n|dry-run' => \$dry_run,
    ) or usage;
    $self->forobj(@ARGV, sub {
        my ($id) = @_;
        print STDERR "disc $id\n\n" if $dry_run;
        my %seen;
        return if !$force && -e 'playlist.pls';
        my @files = glob('*/listen.mp3');
        return if !@files;
        my @tracks = map { basename(dirname($_)) } @files;
        my $fh = xopen('>', 'playlist.pls');
        print $fh "[playlist]\n\n";
        my $n = 0;
        foreach my $t (@tracks) {
            $n++;
            my $mp3 = MP3::Tag->new("$t/listen.mp3");
            my ($title, $track, $artist, $album, $comment, $year, $genre) = $mp3->autoinfo;
            my $seconds = $mp3->total_secs_int;
            print $fh <<"EOS";
File$n=$t/listen.mp3
Title$n=$album - $title / $artist
Length$n=$seconds

EOS
        }
        print $fh <<"EOS";
NumberOfEntries=$n
Version=2
EOS
    });
}

sub cmd_mktags {
    my ($self) = @_;
    my ($dry_run, $force);
    GetOptions(
        'n|dry-run' => \$dry_run,
        'f|force'   => \$force,
    ) or usage;
    $self->forobj(@ARGV, sub {
        my ($id) = @_;
        print STDERR "disc $id\n\n" if $dry_run;
        my ($disc, $tracks) = read_metadata('metadata');
        my @dirs = grep { /^\d+$/ } glob('*');
        foreach my $t (sort keys %$tracks) {
            print STDERR "track $t\n" if $dry_run;
            if (-e "$t/tags" && ! $force) {
                #print STDERR "[\e[32;1mskip\e[0m] $t\n";
                next;
            }
            write_tags("$t/tags", $tracks->{$t}, $disc, $dry_run);
            #print STDERR "[\e[32;1mok  \e[0m] $t\n";
            print STDERR "\n" if $dry_run;
        }
    });
}

sub cmd_edit {
    my ($self) = @_;
    $self->forobj(@ARGV, sub {
        my ($id) = @_;
        my $mtime = mtime('metadata');
        my $editor = $ENV{'VISUAL'} || $ENV{'EDITOR'} || 'vi';
        system($editor, 'metadata') == 0 or fatal 'editor exited with error status';
        if ($mtime == mtime('metadata')) {
            print STDERR "unchanged: $id\n";
            return;
        }
        my ($disc, $tracks) = read_metadata('metadata');
        foreach my $t (sort keys %$tracks) {
            write_tags("$t/tags", $tracks->{$t}, $disc);
            if (-e "$t/listen.mp3") {
                my $mp3 = MP3::Tag->new("$t/listen.mp3");
                my $h = read_tags("$t/tags");
                $mp3->update_tags($h);
            }
        }
    });
}

sub cmd_make {
    # Make a playlist
    # auric make b7 discs/6f10c308/{5..8}
    #   b7
    #   |-- 0001 -> ../discs/6f10c308/0005
    #   |-- 0002 -> ../discs/6f10c308/0006
    #   |-- 0003 -> ../discs/6f10c308/0007
    #   `-- 0004 -> ../discs/6f10c308/0008
    usage if @ARGV < 2;
    my $p = shift @ARGV;
    fatal "Playlist $p already exists" if -e $p;
    my @tracks;
    foreach (@ARGV) {
        m{^(.+)/([0-9]+)$}
            or fatal "I don't know how to make something from $_";
        my ($id, $t) = ($1, $2);
        push @tracks, sprintf('%s/%04d', $id, $t);
    }
    xmkdir($p);
    my $n = 0;
    foreach (@tracks) {
        $n++;
        xsymlink("../$_", sprintf('%s/%04d', $p, $n));
    }
}

sub mtime {
    my ($f) = @_;
    my @stat = stat $f;
    return undef if !@stat;
    return $stat[9];
}

sub call_hook {
    my $name = shift;
    if (-x "$music_dir/.auric/bin/$name" && -f _) {
        system("$music_dir/.auric/bin/$name", @_);
    }
}

sub have_disc {
    my ($self) = @_;
    my @command = ( qw(udevadm info --query property), '--name' => $self->device );
    open my $fh, '-|', @command or fatal "start udevadm: $!";
    while (<$fh>) {
        return 1 if /^ID_CDROM_MEDIA_CD=1/;
    }
    return;
}
### sub wait_for_disc {
###     my ($self) = @_;
###     return if $self->have_disc;
###     my $dev = $self->device;
###     my @command = qw(udevadm monitor --udev --property);
###     open my $fh, '-|', @command or fatal "Can't run udevadm: $!";
###     while (<$fh>) {
###         return if /^DEVLINKS=(?:\S+ )*$dev(?: |$)/;
###     }
###     fatal;
### }

sub _ingest_disc {
    my ($self, $disc, %opt) = @_;
    my $metadata = "$disc/metadata";
    my $dest     = "$disc/cdda2wav";
    my @inf_files = glob("$disc/audio_*.inf");
    # $disc/audio_*.inf --> $dist/cddawav/*.inf
    return if !@inf_files;
    return if !$opt{'force'} && -e $metadata;
    $opt{'dry_run'} or xmkdir($dest);
    my %disc = ( 'id' => '.' );
    my (@tracks, %tags, @masters, @listens, $fh);
    foreach my $inf (@inf_files) {
        next if $inf !~ m{/audio_(\d\d)\.inf$};
        my $t = $1;
        my $tnum = sprintf('%04d', $t);
        my $tdir = "$disc/$tnum";
        my $tags = MP3::Tag->new($inf);
        my ($title, $tracknum, $artist, $album, $comment, $year, $genre) = $tags->autoinfo;
        $disc{'album' }{$album } = 1;
        $disc{'year'  }{$year  } = 1;
        $disc{'genre' }{$genre } = 1;
        $disc{'artist'}{$artist} = 1;
        push @tracks, {
            'id'     => $tnum,
            'title'  => $title,
            'track'  => $tracknum,
            'artist' => $artist,
        };
        next if $opt{'dry_run'};
        xmkdir($tdir);
        xrename($inf, "$disc/cdda2wav/".basename($inf));
        my $wav = "$disc/audio_$t.wav";
        if (-e $wav) {
            my $master = "$tdir/master.wav";
            xrename($wav, $master);
            push @masters, $master;
            my $link = "$disc/cdda2wav/".basename($wav);
            -e $link or symlink "../$tnum/master.wav", $link or fatal "create symlink $link to $disc/$tnum/master.wav: $!";
        }
        my $mp3 = "$disc/audio_$t.mp3";
        my $listen = "$tdir/listen.mp3";
        if (-e $mp3 && !-e $listen) {
            xrename($mp3, $listen);
            push @listens, $listen;
            my $link = "$disc/cdda2wav/".basename($mp3);
            -e $link or symlink "../$tnum/listen.mp3", $link or fatal "Can't create symlink $link to $disc/$tnum/listen.mp3: $!";
        }
    }
    if (!$opt{'dry_run'}) {
        $fh = xopen('>', $metadata);
        select $fh;
        foreach (grep { -e "$disc/$_" } qw(audio.cddb audio.cdindex)) {
            xrename("$disc/$_", "$dest/$_");
        }
        if ($opt{'encode'} && @masters) {
            my @mp3s = $self->_encode($disc, @masters);
            foreach (sort { $a cmp $b } keys %tags) {
                1;
            }
        }
    }
    my %order;
    foreach my $m (\%disc, @tracks) {
        my $oid = delete $m->{'id'};
        my @keys = sort { ($order{$a} ||= 99) <=> ($order{$b} ||= 99) || $a cmp $b } keys %$m;
        foreach my $key (@keys) {
            my $val = $m->{$key};
            if (ref $val) {
                print "$oid $key $_\n" for sort keys %$val;
            }
            else {
                print "$oid $key $val\n";
            }
        }
        print "\n";
    }
    return 1;
}

sub cdda2wav {
    my ($self, $fhlog) = @_;
    my $dev = $self->config('device');
    my @cmd = (
        'cdda2wav',
            '-D' => $dev,
            '-x',
            '-vsummary,titles',
            '-cddb',
            1,
            '-B',
            '-no-hidden-track',
            'out-fd=1'
    );
    print STDERR "\e[36;1m@cmd\e[0m\n" if DEBUG;
    my $pid = open my $fh, '-|', @cmd
        or fatal "start cdda2wav: $!";
    $fh->autoflush(1);
    my $fhparse = xopen('>', 'cdda2wav.parse');
    $fhparse->autoflush(1);
    my $discid = '-';
    my @title;
    my ($num_tracks, $started);
    while (<$fh>) {
        print STDERR "\e[31;1m  | $_\e[0m" if DEBUG;
        print $fhlog $_ if $fhlog;
        print $fhparse "disc prep\n" if !$started;
        $started = 1;
        if (/AUDIOtrack/) {
            my $info = <$fh>;
            $num_tracks = $1 if $info =~ /^\s*1-\s*(\d+)/;
        }
        if (/^CDDB discid: 0x([0-9a-f]{8})/) {
            $discid = $1;
        }
        elsif (/^Album title: +'(.+)'(.*)/) {
            $title[0] = $1 . $2;
            $title[0] =~ s/\t.*//;
        }
        elsif (/^Track +(\d+): '(.+)'/ && $1 > 0) {
            $title[$1] = $2;
            $num_tracks = $1 if $1 > $num_tracks;
        }
        elsif (/^percent_done:|^recording /) {
            @title = ('[Untitled disc]') if !@title;
            print STDERR "\e[35;1m$discid \e[36;1m$title[0]\e[0m\n";
            print $fhparse "disc begin $discid $num_tracks $title[0]\n";
            my $n = 1;
            my $t = $title[$n] ||= '[Untitled track]';
            printf STDERR "       %3d. %s", $n, $t;
            local $/ = "\r";
            while (<$fh>) {
                if (/100%/) {
                    my $result;
                    if (/track +(\d+) .*recorded (.+)/) {
                        my (undef, $adv) = ($1, $2);
                        my $color = 32;
                        if ($adv eq 'successfully') {
                            $result = ' ok ';
                        }
                        elsif ($adv =~ /with minor problems/) {
                            $result = ' ok ';
                        }
                        else {
                            $result = 'warn';
                            $color = 33;
                        }
                        printf STDERR "\r  \e[${color};1m%-4.4s\e[0m %3d. %s \n", $result, $n, $t;
                        my $ignore1 = <$fh>;
                        my $ignore2 = <$fh>;
                        $result =~ s/^ +| +$//g;
                    }
                    print $fhparse "track $n done $result\n";
                    $t = $title[++$n] ||= '[Untitled track]';
                    print $fhparse "track $n begin $t\n" if $n <= $num_tracks;
                }
                elsif (/(\d+)%/) {
                    print $fhparse "track $n progress $1\n";
                    printf STDERR "\r  %3d%% %3d. %s ", $1, $n, $t;
                }
            }
        }
        elsif (/\r100% +track\s+(\d+) '(.+)' recorded (.+)/) {
            my ($n, $t, $r) = ($1, $2, $3);
            $title[$n] ||= $t;
            my ($result, $color) = (' ok ', 32);
            if ($r !~ /successful/ && $r !~ /with minor/) {
                ($result, $color) = ('warn', 33);
            }
            printf STDERR "\r  \e[${color};1m%-4.4s\e[0m %3d. %s \n", $result, $n, $t;
            $result =~ s/^ +| +$//g;
            print $fhparse "track $n done $result\n";
        }
    }
    #waitpid($pid, 0);
    close $fh
        or print STDERR "cdda2wav may have failed ($?): $!";
    return $discid;
}

sub read_tags {
    my ($f) = @_;
    my %tag;
    my $fh = xopen('<', $f);
    while (<$fh>) {
        chomp;
        next if /^\s*(?:#.*)?$/;  # Skip blank lines and comments
        my ($key, $val) = split /\s+/, $_, 2;
        $tag{$key} = $val;
    }
    close $fh;
    return \%tag;
}

sub read_metadata {
    my ($f) = @_;
    my (%disc, %track);
    my $fh = xopen('<', $f);
    while (<$fh>) {
        if (/^\. (\S+) (.*)/) {
            $disc{$1} = $2;
        }
        elsif (/^(\d+) (\S+) (.*)/) {
            $track{$1}{$2} = $3;
            $track{$1}{'id'} ||= $1;
        }
    }
    $_->{'album'} ||= $disc{'title'} || $disc{'album'} for values %track;
    close $fh;
    return \%disc, \%track;
}

sub write_tags {
    my ($f, $track, $disc, $dry_run) = @_;
    my $perm = 0644;
    my $fh;
    if (!$dry_run) {
        if (-e $f) {
            $perm = (stat $f)[2] & 07777;
            chmod($perm | 0600, $f);
        }
        $fh = xopen('>', $f);
        select $fh;
    }
    my %keys = map { $_ => 1 } (keys %$track, keys %$disc);
    foreach my $key (sort keys %keys) {
        my ($val) = grep { defined $_ } $track->{$key}, $disc->{$key};
        print "$key $val\n" if defined $val;
    }
    if (!$dry_run) {
        chmod($perm & 0444, $fh);
        close $fh;
    }
}
sub dirname {
    my ($file) = @_;
    $file =~ s{/[^/]+$}{};
    return $file;
}

sub basename {
    my ($file) = @_;
    $file =~ s{.+/}{};
    return $file;
}

sub min {
    return 0 if !@_;
    my $n = 2**32;
    foreach (@_) {
        $n = $_ if $_ < $n;
    }
    return $n;
}

sub max {
    my $n = 0;
    foreach (@_) {
        $n = $_ if $_ > $n;
    }
    return $n;
}

sub xmkdir {
    my ($dir) = @_;
    -d $dir or mkdir $dir or fatal "mkdir $dir $!";
}

sub xrename {
    my ($s, $d) = @_;
    rename $s, $d or fatal "rename $s to $d: $!";
}

sub xchdir {
    my ($d) = @_;
    chdir $d or fatal "chdir $d: $!";
}

sub xopen {
    my ($mode, $file) = @_;
    my $fh;
    return $fh if open $fh, $mode, $file;
    fatal "open $file: $!" if $mode =~ /\+/;
    fatal "open $file for reading: $!" if $mode =~ /</;
    fatal "open $file for writing: $!" if $mode =~ />/;
    fatal "start $file: $!" if $mode =~ /\|/;
    fatal "open $file: $!";
}

sub xsymlink {
    my ($old, $new) = @_;
    symlink $old, $new or fatal "symlink $old $new: $!";
}

sub usage {
    print STDERR "usage: $prog COMMAND [ARG...]\n";
    exit 1;
}

sub fatal {
    print STDERR "$prog: @_\n";
    exit 2;
}

1;

__END__
my %cd=get_cddb(\%config);
        unless(defined $cd{title}) {
          die "no cddb entry found";
        }

        # do somthing with the results

        print "artist: $cd{artist}\n";
        print "title: $cd{title}\n";
        print "category: $cd{cat}\n";
        print "cddbid: $cd{id}\n";
        print "trackno: $cd{tno}\n";
        my $n=1;
        foreach my $i ( @{$cd{track}} ) {
          print "track $n: $i\n";
          $n++;
        }

