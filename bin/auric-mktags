#!/usr/bin/perl 

use strict;
use warnings;

use File::Glob;
use MP3::Tag;
use Getopt::Long
    qw(:config posix_default gnu_compat require_order bundling no_ignore_case);

my $root = $ENV{MUSIC} ||= "$ENV{HOME}/music";
my ($dry_run, $force);

GetOptions(
    'n|dry-run' => \$dry_run,
    'f|force'   => \$force,
) or exit usage();

chdir $root or die "Can't change to root directory $root: $!";
my @discs = map { s{^($root/)?disc-}{}o; $_ } @ARGV ? @ARGV : glob('disc-*');

foreach my $id (@discs) {
    chdir "$root/disc-$id" or die "Can't chdir to disc-$id: $!";
    print STDERR "disc $id\n\n" if $dry_run;
    my ($disc, $tracks) = read_metadata('metadata');
    my @dirs = grep { /^\d+$/ } glob('*');
    foreach my $t (sort keys %$tracks) {
        print STDERR "track $t\n" if $dry_run;
        if (-e "$t/tags" && ! $force) {
            #print STDERR "[\e[32;1mskip\e[0m] $t\n";
            next;
        }
        write_tags("$t/tags", $tracks->{$t}, $disc);
        #print STDERR "[\e[32;1mok  \e[0m] $t\n";
        print STDERR "\n" if $dry_run;
    }
}

sub read_metadata {
    my ($f) = @_;
    my (%disc, %track);
    open my $fh, '<', $f or die "Can't open file $f: $!";
    while (<$fh>) {
        if (/^\. (\S+) (.*)/) {
            $disc{$1} = $2;
        }
        elsif (/^(\d+) (\S+) (.*)/) {
            $track{$1}{$2} = $3;
            $track{$1}{'id'} ||= $1;
        }
    }
    $_->{'album'} ||= $disc{'title'} || $disc{'album'} for values %track;
    close $fh;
    return \%disc, \%track;
}

sub write_tags {
    my ($f, $track, $disc) = @_;
    my $perm = 0644;
    my $fh;
    if (!$dry_run) {
        if (-e $f) {
            $perm = (stat $f)[2] & 07777;
            chmod($perm | 0600, $f);
        }
        open $fh, '>', $f or die "Can't open file $f for writing: $!";
        select $fh;
    }
    my %keys = map { $_ => 1 } (keys %$track, keys %$disc);
    foreach my $key (sort keys %keys) {
        my ($val) = grep { defined $_ } $track->{$key}, $disc->{$key};
        print "$key $val\n" if defined $val;
    }
    if (!$dry_run) {
        chmod($perm & 0444, $fh);
        close $fh;
    }
}
